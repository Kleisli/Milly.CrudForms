# This is a trimmed-down version of Neos.Neos:Page, usable for standalone usage in Flow applications.
#
# The "public API", like "head.titleTag.content", "body", or "body.javascripts" is exactly the same as
# in Neos.Neos:Page.

prototype(Milly.CrudUI:Webpage) < prototype(Neos.Fusion:Component) {
    # add the DocType as processor; because it will break AFX.
    @process.addDocType = ${'<!DOCTYPE html>' + value}

    headerComment = ${Configuration.setting('Milly.CrudUI.headerComment')}

    head = Neos.Fusion:Join {
        titleTag = Neos.Fusion:Tag {
            tagName = 'title'
        }
        # Script and CSS includes in the head should go here
        stylesheets = Neos.Fusion:Join {
          frontend = Neos.Fusion:Tag {
            tagName = 'link'
            attributes {
              rel = 'stylesheet'
              href = Neos.Fusion:ResourceUri {
                path = 'resource://Milly.CrudUI/Public/Css/frontend.css'
              }
            }
          }
        }
        javascripts = Neos.Fusion:Join {
          tailwind = '<script src="https://cdn.tailwindcss.com?plugins=forms,typography"></script>'
        }
    }

    body = Neos.Fusion:Join {
        # Script includes before the closing body tag should go here
        javascripts = Neos.Fusion:Join
        # This processor appends the rendered javascripts Array to the rendered template
        @process.appendJavaScripts = ${value + this.javascripts}
    }

    renderer = afx`
        <html lang="de">
        {props.headerComment}
        <head>
            <meta charset="UTF-8" />
            {props.head}
        </head>
        <body>
        {props.body}
        </body>
        </html>
    `


    # enable Neos.Fusion:Debug helper
    @process.debugDump = Neos.Fusion:DebugDump
}


