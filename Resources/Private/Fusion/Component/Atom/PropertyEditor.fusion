prototype(Milly.CrudUI:Component.PropertyEditor) < prototype(Neos.Fusion:Component) {

    propertyName = ''
    propertyConfig = ${[]}
    object = null

    renderer = Neos.Fusion:Case {
        readonly {
            condition = ${props.propertyConfig.readonly}
            renderer = Milly.CrudUI:Component.PropertyDisplayer {
                @apply.props = ${props}
            }
        }
        customDisplayer {
            condition = ${props.propertyConfig.ui.editor}
            type = ${props.propertyConfig.ui.editor}
            element.@apply.props = ${props}
        }
        default {
            condition = true
            renderer = Neos.Fusion:Match {
                @subject = ${props.propertyConfig.type}

                textarea = Neos.Fusion.Form:Textarea {
                    attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                        path = 'propertyEditor.textarea.class'
                    }
                }
                number = Milly.CrudUI:Component.PropertyEditor.Number
                email = Milly.CrudUI:Component.PropertyEditor.Email
                select = Milly.CrudUI:Component.PropertyEditor.Select
                multiSelect = Milly.CrudUI:Component.PropertyEditor.MultiSelect
                booleanRadio = Milly.CrudUI:Component.PropertyEditor.BooleanRadio
                booleanCheckbox = Milly.CrudUI:Component.PropertyEditor.BooleanCheckbox
                dateTime = Milly.CrudUI:Component.PropertyEditor.DateTime
                date = Milly.CrudUI:Component.PropertyEditor.Date
                image = Milly.CrudUI:Component.PropertyEditor.Image
                audio = Milly.CrudUI:Component.PropertyEditor.Audio
                dateInterval = Milly.CrudUI:Component.PropertyEditor.DateInterval
                jsonList = Milly.CrudUI:Component.PropertyEditor.JsonList

                @default = Neos.Fusion.Form:Textfield {
                    attributes {
                        disabled = ${props.propertyConfig.readonly ? true : false}
                        class = Milly.CrudUI:Helper.ThemeRenderer {
                            path = 'propertyEditor.textfield.class'
                        }
                    }
                }
            }
        }
    }
}
