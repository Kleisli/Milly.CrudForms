prototype(Milly.Crudforms:Component.PropertyEditor) < prototype(Neos.Fusion:Component) {

    propertyName = ''
    propertyConfig = ${[]}
    object = null

    renderer = Neos.Fusion:Case {
        readonly {
            condition = ${props.propertyConfig.ui.readonly}
            renderer = Milly.Crudforms:Component.PropertyDisplayer {
                @apply.props = ${props}
            }
        }
        customDisplayer {
            condition = ${props.propertyConfig.ui.editor}
            type = ${props.propertyConfig.ui.editor}
            element.@apply.props = ${props}
        }
        default {
            condition = true
            renderer = Neos.Fusion:Match {
                @subject = ${props.propertyConfig.type}

                textarea = Neos.Fusion.Form:Textarea {
                    attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                        path = 'propertyEditor.textarea.class'
                    }
                }
                number = Milly.Crudforms:Component.PropertyEditor.Number
                email = Milly.Crudforms:Component.PropertyEditor.Email
                select = Milly.Crudforms:Component.PropertyEditor.Select
                multiSelect = Milly.Crudforms:Component.PropertyEditor.MultiSelect
                booleanRadio = Milly.Crudforms:Component.PropertyEditor.BooleanRadio
                booleanCheckbox = Milly.Crudforms:Component.PropertyEditor.BooleanCheckbox
                dateTime = Milly.Crudforms:Component.PropertyEditor.DateTime
                date = Milly.Crudforms:Component.PropertyEditor.Date
                image = Milly.Crudforms:Component.PropertyEditor.Image
                audio = Milly.Crudforms:Component.PropertyEditor.Audio
                dateInterval = Milly.Crudforms:Component.PropertyEditor.DateInterval
                jsonList = Milly.Crudforms:Component.PropertyEditor.JsonList

                @default = Neos.Fusion.Form:Textfield {
                    attributes {
                        disabled = ${props.propertyConfig.readonly ? true : false}
                        class = Milly.Crudforms:Helper.ThemeRenderer {
                            path = 'propertyEditor.textfield.class'
                        }
                    }
                }
            }
        }
    }
}
