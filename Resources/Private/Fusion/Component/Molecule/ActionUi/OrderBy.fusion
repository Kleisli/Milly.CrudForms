prototype(Milly.Crudforms:Component.ActionUi.OrderBy) < prototype(Neos.Fusion:Component) {

    propertyName = ''
    modelClassName = ''
    crudFormsView = 'index'
    orderBy = ${propertyConfig.propertyPath ? propertyConfig.propertyPath : this.propertyName}

    renderer = Neos.Fusion:Link.Action {
        class = "white-link"
        href.action = "index"
        href.package = ${Milly.Mapping.getPackageNameByModel(props.modelClassName)}
        href.controller = ${Milly.Mapping.getControllerNameByClass(Milly.Mapping.getControllerClassByModel(props.modelClassName))}
        href.arguments = ${Array.concat({filter: filterValues}, {orderBy: props.orderBy}, {paginationPageSize: pagination.pageSize}, {orderDirection: (orderBy == props.orderBy && orderDirection=='ASC' ? 'DESC' : 'ASC')})}
        @if.hasOrderBy = ${Milly.CrudForms.getConfigurationByClass(props.modelClassName, 'views.'+props.crudFormsView+'.orderBy') && Carbon.Array.hasValue(Milly.CrudForms.getConfigurationByClass(props.modelClassName, 'views.'+props.crudFormsView+'.orderBy'), props.propertyName)}

        content = Neos.Fusion:Join {
            label = Neos.Fusion:Case {
                property {
                    condition = ${props.propertyName != 'label'}
                    renderer = Milly.Crudforms:Component.Label.PropertyLabel {
                        modelClassName = ${props.modelClassName}
                        propertyName = ${props.propertyName}
                        propertyConfig = ${propertyConfig}
                    }
                }
                default {
                    condition = true
                    renderer = Milly.Crudforms:Component.Label.ClassLabel {
                        className = ${props.modelClassName}
                        showClassLabel = 'one'
                    }
                }
            }

            iconActive = Neos.Fusion:Value {
                @if.isActive = ${orderBy == props.orderBy}

                value = Neos.Fusion:Join {
                    text = '&nbsp;'
                    icon = Neos.Fusion:Match {
                        @subject = ${orderDirection}
                        @default = Milly.CrudForms:Atom.Icon.Sorting.SortUp
                        ASC = Milly.CrudForms:Atom.Icon.Sorting.SortDown
                    }
                }
            }
            iconInactive = Neos.Fusion:Value {
                @if.isActive = ${orderBy != props.orderBy}
                value = Neos.Fusion:Join {
                  text = '&nbsp;'
                  icon = Milly.CrudForms:Atom.Icon.Sorting.Sort
                }
            }
        }
    }
}
