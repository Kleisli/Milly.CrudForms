prototype(Milly.CrudUI:Component.ActionUi.OrderBy) < prototype(Neos.Fusion:Component) {

    propertyName = ''
    modelClassName = ''
    CrudUIView = 'index'
    orderBy = ${propertyConfig.propertyPath ? propertyConfig.propertyPath : this.propertyName}

    renderer = Neos.Fusion:Link.Action {
        class = "white-link"
        href.action = "index"
        href.package = ${Milly.Mapping.getControllerPackageNameByModel(props.modelClassName)}
        href.controller = ${Milly.Mapping.getControllerNameByModel(props.modelClassName)}
        href.arguments = ${Array.concat({filter: filterValues}, {orderBy: props.orderBy}, {paginationPageSize: pagination.pageSize}, {orderDirection: (orderBy == props.orderBy && orderDirection=='ASC' ? 'DESC' : 'ASC')})}
        @if.hasOrderBy = ${Milly.CrudUI.getConfigurationByModel(props.modelClassName, 'views.'+props.CrudUIView+'.orderBy') && Milly.Array.hasValue(Milly.CrudUI.getConfigurationByModel(props.modelClassName, 'views.'+props.CrudUIView+'.orderBy'), props.propertyName)}

        content = Neos.Fusion:Join {
            label = Neos.Fusion:Case {
                property {
                    condition = ${props.propertyName != 'label'}
                    renderer = Milly.CrudUI:Component.Label.PropertyLabel {
                        modelClassName = ${props.modelClassName}
                        propertyName = ${props.propertyName}
                        propertyConfig = ${propertyConfig}
                    }
                }
                default {
                    condition = true
                    renderer = Milly.CrudUI:Component.Label.ClassLabel {
                        className = ${props.modelClassName}
                        showClassLabel = 'one'
                    }
                }
            }

            iconActive = Neos.Fusion:Value {
                @if.isActive = ${orderBy == props.orderBy}

                value = Neos.Fusion:Join {
                    text = '&nbsp;'
                    icon = Neos.Fusion:Match {
                        @subject = ${orderDirection}
                        @default = Milly.CrudUI:Atom.Icon.Sorting.SortUp
                        ASC = Milly.CrudUI:Atom.Icon.Sorting.SortDown
                    }
                }
            }
            iconInactive = Neos.Fusion:Value {
                @if.isActive = ${orderBy != props.orderBy}
                value = Neos.Fusion:Join {
                  text = '&nbsp;'
                  icon = Milly.CrudUI:Atom.Icon.Sorting.Sort
                }
            }
        }
    }
}
