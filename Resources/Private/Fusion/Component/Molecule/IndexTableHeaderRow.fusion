prototype(Milly.Crudforms:Component.IndexTableHeaderRow) < prototype(Neos.Fusion:Component) {

    crudFormsView = 'index'
    modelClassName = ''

    renderer = afx`
        <tr>
            <th>
                <Milly.Crudforms:Component.ActionUi.OrderBy propertyName='label' modelClassName={props.modelClassName} />

                <Neos.Fusion.Form:Form form.data.filter={props.filterValues} form.target.action="index" attributes.class={'filter'} attributes.id={String.md5(props.modelClassName)+'-filter'}/>
                <Neos.Fusion:Fragment @if.hasFilterType={Milly.CrudForms.getConfigurationByModel(props.modelClassName, 'views.'+props.crudFormsView+'.filter.label.type')}>
                    <Neos.Fusion.Form:Form form.data.filter={props.filterValues} form.target.action="index">
                        <Milly.Crudforms:Component.PropertyFilter filterValues={props.filterValues} propertyName='label' modelClassName={props.modelClassName} />
                    </Neos.Fusion.Form:Form>
                </Neos.Fusion:Fragment>
            </th>
            <Neos.Fusion:Loop items={Milly.CrudForms.getConfigurationByModel(props.modelClassName, 'properties', props.crudFormsView)} itemKey="propertyName" itemName="propertyConfig">
                <th style="vertical-align: top;">
                    <Milly.Crudforms:Component.Label.PropertyLabel propertyName={propertyName} propertyConfig={propertyConfig} modelClassName={props.modelClassName}
                                                                   @if.notHasOrderBy={!Milly.CrudForms.getConfigurationByModel(props.modelClassName, 'views.'+props.crudFormsView+'.orderBy') || !Carbon.Array.hasValue(Milly.CrudForms.getConfigurationByModel(props.modelClassName, 'views.'+props.crudFormsView+'.orderBy'), propertyName)}/>

                    <Milly.Crudforms:Component.ActionUi.OrderBy propertyName={propertyName} modelClassName={props.modelClassName} />

                    <Neos.Fusion:Fragment @if.hasFilterType={Milly.CrudForms.getConfigurationByModel(props.modelClassName, 'views.'+props.crudFormsView+'.filter.'+propertyName+'.type')}>
                        <br/>
                        <Neos.Fusion.Form:Form form.data.filter={props.filterValues} form.target.action="index">
                            <Milly.Crudforms:Component.PropertyFilter filterValues={props.filterValues} propertyName={propertyName} propertyConfig={propertyConfig} modelClassName={props.modelClassName}/>
                        </Neos.Fusion.Form:Form>
                    </Neos.Fusion:Fragment>
                </th>
            </Neos.Fusion:Loop>
            <th class="neos-action" style="vertical-align: top;">
                <Neos.Fusion:Fragment @if={false}>
                    <span class={"neos-button neos-pull-right"+(filterValues?" neos-active":"")} id="toggle-filter">
                        <Milly.CrudForms:Atom.Icon.Filter />
                    </span>
                </Neos.Fusion:Fragment>
            </th>
        </tr>
    `

}
