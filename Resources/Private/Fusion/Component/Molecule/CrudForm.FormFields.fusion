prototype(Milly.Crudforms:Component.Fields) < prototype(Neos.Fusion:Component) {

    translation = ''

    renderer = Neos.Fusion:Loop {
        items = ${props.properties}
        itemKey = "propertyName"
        itemName = "propertyConfig"

        itemRenderer = Neos.Fusion:Match {
            @subject = ${propertyConfig.type}
            textarea = afx`
                    <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                        <Neos.Fusion.Form:Textarea />
                    </Milly.Crudforms:Component.FieldContainer>
                `

            number  = afx`
                    <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                        <Neos.Fusion.Form:Textfield attributes.type="number"/>
                    </Milly.Crudforms:Component.FieldContainer>
                `

            select = afx`
                <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                    <Neos.Fusion.Form:Select>
                        <Neos.Fusion:Loop items={Milly.CrudForms.getFieldOptions(propertyConfig.options)}>
                            <Neos.Fusion.Form:Select.Option option.value={itemKey}>
                                {item}
                            </Neos.Fusion.Form:Select.Option>
                        </Neos.Fusion:Loop>
                    </Neos.Fusion.Form:Select>
                </Milly.Crudforms:Component.FieldContainer>
            `

            booleanRadio = afx`
                <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                        <Neos.Fusion:Loop items={Milly.CrudForms.getFieldOptions(propertyConfig.options)}>

                                <label class="neos-radio" for={field.getName() + '-' + itemKey}>
                                    <Neos.Fusion.Form:Radio field.value={itemKey}
                                                            attributes.checked={(field.getCurrentValueStringified() ==  String.toString(itemKey)) || (field.getCurrentValueStringified() == '' && itemKey == 0)}
                                                            attributes.id={field.getName() + '-' + field.getTargetValueStringified()}/><span></span>
                                    {item}
                                </label>

                        </Neos.Fusion:Loop>
                </Milly.Crudforms:Component.FieldContainer>
            `

            booleanCheckbox = afx`
                <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object'+ (props.translation ? '[translations]['+props.translation+']' : '') + '['+propertyName+']'}
                                                          label={propertyConfig.label ? propertyConfig.label : propertyName}>
                    <label class="neos-checkbox" for={field.getName()}>
                        <Neos.Fusion.Form:Checkbox field.value="1"
                                                   attributes.id={field.getName()}/><span></span>
                        {propertyConfig.label ? propertyConfig.label : propertyName}
                    </label>

                </Milly.Crudforms:Component.FieldContainer>
            `

            dateTime  = afx`
                <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                    <input type="datetime-local" name={field.getName() + '[date]'} value={Date.format(field.getCurrentValue(), 'Y-m-d\TH:i')} />
                    <input type="hidden" name={field.getName() + '[dateFormat]'}  value="Y-m-d\\TH:i" />
                </Milly.Crudforms:Component.FieldContainer>
            `

            date  = afx`
                <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object['+propertyName+']'} label={propertyConfig.label ? propertyConfig.label : propertyName}>
                    <input type="date" name={field.getName() + '[date]'} value={Date.format(field.getCurrentValue(), 'Y-m-d')} />
                    <input type="hidden" name={field.getName() + '[dateFormat]'}  value="Y-m-d" />
                </Milly.Crudforms:Component.FieldContainer>
            `

            @default = afx`
                    <Milly.Crudforms:Component.FieldContainer type={propertyConfig.type} field.name={'object'+ (props.translation ? '[translations]['+props.translation+']' : '') + '['+propertyName+']'}
                                                              label={propertyConfig.label ? propertyConfig.label : propertyName}>
                        <Neos.Fusion.Form:Textfield />
                    </Milly.Crudforms:Component.FieldContainer>
                `

        }
    }
}
