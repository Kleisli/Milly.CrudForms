prototype(Milly.Crudforms:Component.PropertyEditors) < prototype(Neos.Fusion:Component) {

    object = null
    properties = ${[]}
    translation = ''

    renderer = Neos.Fusion:Loop {
        items = ${props.properties}
        itemKey = "propertyName"
        itemName = "propertyConfig"

        itemRenderer = Milly.Crudforms:Helper.PropertyEditorContainer {
            type = ${propertyConfig.type}
            field.name = Neos.Fusion:Case {
                custom {
                    condition = ${propertyConfig.fieldName}
                    renderer = ${propertyConfig.fieldName}
                }
                default {
                    condition = true
                    renderer = ${'object['+ (propertyConfig.propertyPath ? String.replace(propertyConfig.propertyPath, '.', '][') : propertyName) +']'}
                }
            }
            field.value = ${Milly.Object.getProperty(props.object, propertyConfig.propertyPath ? propertyConfig.propertyPath : propertyConfig._propertyName)}

            field.multiple = ${propertyConfig.type == 'multiSelect'}
            label = Milly.Crudforms:Component.Label.PropertyLabel {
                modelClassName = ${Type.className(props.object)}
                propertyName = ${propertyName}
                propertyConfig = ${propertyConfig}
            }

            content = Milly.Crudforms:Component.PropertyEditor {
                propertyName = ${propertyName}
                propertyConfig = ${propertyConfig}
                object = ${props.object}
            }
        }
        itemRenderer.@if.notReadonly = ${!propertyConfig.ui.readonly}
    }
}
