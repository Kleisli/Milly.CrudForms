prototype(Milly.Crudforms:Component.NewForm) < prototype(Neos.Fusion:Component) {

    properties = ${[]}
    preset = Neos.Fusion:DataStructure
    parent = null
    parentProperty = null

    renderer = Neos.Fusion.Form:Form {
        form.target.action = "create"
        attributes.id = "new"
        content = Neos.Fusion:Tag {
            attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                classPath = 'template.new.form.class'
            }
            content = Neos.Fusion:Join {
                presetFields = Neos.Fusion:Loop {
                    items = ${props.preset}
                    itemRenderer = Neos.Fusion.Form:Hidden {
                        field.name = ${"object["+itemKey+"]"}
                        attributes.value = ${item}
                    }
                }
                parentField = Neos.Fusion.Form:Hidden {
                    @if.hasParent = ${props.parent}
                    field.name = ${"object["+ props.parentProperty +"]"}
                    attributes.value = ${props.parent}
                }
                editors = Milly.Crudforms:Component.PropertyEditors {
                    properties = ${props.properties}
                }
            }
        }
    }
}
