prototype(Milly.CrudUI:Template.Edit) < prototype(Neos.Fusion:Component) {

    @context {
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudUI.defaultTheme'))}
    }

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            content = Milly.CrudUI:Component.Label.ObjectLabel {
                object = ${object}
                showClassLabel = 'one'
            }
        }
    }

    main = Neos.Fusion:DataStructure {
        first = Neos.Fusion:Join {
            editForm = Milly.CrudUI:Component.EditForm {
                object = ${object}
                properties = ${Milly.CrudUI.getConfigurationByModel(object, 'properties', 'edit')}
            }
        }
        second = Neos.Fusion:Join {
            relations = Milly.CrudUI:Component.ShowRelations {
                object = ${object}
                relations = ${Milly.CrudUI.getConfigurationByModel(object, 'relations', 'edit')}
            }
        }
    }

    footer = Neos.Fusion:Join {
        updateButton = Milly.CrudUI:Component.ActionUi.UpdateButton
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
            path = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.first.class'
                        }
                        content = ${props.main.first}
                    }
                    second = Neos.Fusion:Tag {
                        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.second.class'
                        }
                        content = ${props.main.second}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
    }
}
