prototype(Milly.CrudUI:Template.Translate) < prototype(Neos.Fusion:Component) {

    @context {
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudUI.defaultTheme'))}
    }

    object = ${object}

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            content = Milly.CrudUI:Component.Label.ObjectLabel {
                object = ${object}
                showClassLabel = 'one'
            }
        }
    }

    main = Neos.Fusion:DataStructure {
        first = Neos.Fusion:Join {
            availableLOcales = Neos.Fusion:Loop {
                items = ${Milly.CrudUI.getConfigurationByModel(object, 'views.translate.availableLocales', 'translate')}
                itemRenderer = Milly.CrudUI:Component.ActionUi.Translate {
                    locale = ${item}
                    object = ${object}
                    showLabel = true
                }
            }
            editForm = Milly.CrudUI:Component.EditForm {
                properties = ${Milly.CrudUI.getConfigurationByModel(object, 'properties', 'translate')}
                targetAction = "updateTranslation"
                additionalFormContent.locale = Neos.Fusion.Form:Hidden {
                    field {
                        name = 'locale'
                        name.@process.addArgumentNamespace = ${request.argumentNamespace + '[' + value + ']'}
                        name.@process.addArgumentNamespace.@if.isSet = ${request.argumentNamespace != ''}
                    }
                    attributes.value = ${locale}
                }
                object = ${object}
                @if.localeIsSet = ${locale}
            }
        }
        second = Neos.Fusion:Join {
            default = Milly.CrudUI:Component.ShowProperties {
                object = ${Traduki.Entity.reloadInDefaultLocale(object)}
            }
        }
    }

    footer = Neos.Fusion:Join {
        saveButton = Neos.Fusion.Form:Button {
            attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                path = 'template.edit.saveButton.class'
            }
            attributes.form = "edit"
            content = Neos.Fusion:Join {
                icon = Milly.CrudUI:Atom.Icon.Save
                text = '&nbsp;Save'
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
            path = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.first.class'
                        }
                        content = ${props.main.first}
                    }
                    second = Neos.Fusion:Tag {
                        attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.second.class'
                        }
                        content = ${props.main.second}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.CrudUI:Helper.ThemeRenderer {
                    path = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
        @process.debugDump = Neos.Fusion:DebugDump
    }

}
