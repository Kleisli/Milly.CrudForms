prototype(Milly.Crudforms:Template.Index) < prototype(Neos.Fusion:Component) {

    @propTypes {
        style = ${PropTypes.oneOf(['narrow', 'wide'])}
        pagination = ${PropTypes.arrayOf(PropTypes.getAny())}
        objects = ${PropTypes.arrayOf(PropTypes.getAny())}
    }

    @context {
        crudFormsModelClass = ${crudFormsModelClass}
        configuration = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, null, 'index')}
        crudFormsView = 'index'
    }

    style = 'wide'

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            attributes.style = "display: flex; justify-content: space-between;"
            content = ${configuration.label.many ? configuration.label.many : configuration.label.one + ' (' + objectCount + ')'}
        }
    }

    main = Neos.Fusion:Join {
        indexTable = Milly.Crudforms:Component.IndexTable {
            objects = ${objects}
            disablePagination = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, 'views.index.options.pagination.disable')}
        }
    }

    footer = Neos.Fusion:Join {
        newButton = Milly.Crudforms:Component.ActionUi.New {
            @if.notReadonly = ${!configuration.ui.readonly}
            object = ${object}
            showLabel = true
            modelClassName = ${crudFormsModelClass}
        }
        export = Neos.Fusion:Link.Action {
            class = "neos-button"
            href.action = "index"
            href.format = 'csv'
            href.arguments {
                paginationPageSize = 0
                paginationCurrentPage = 0
                filter = ${filterValues}
                orderBy = ${orderBy}
                orderDirection = ${orderDirection}
            }
            content = Neos.Fusion:Join {
              icon = Milly.CrudForms:Atom.Icon.Download
              text = '&nbsp;&nbsp;CSV'
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = "neos-content neos-fluid-container neos-indented"
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = "neos-row-fluid"
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = ${props.style=='narrow' ? 'neos-span6' : 'neos-span12'}
                        content = ${props.main}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = "neos-footer"
                content = ${props.footer}
            }
        }
        @process.debugDump = Neos.Fusion:DebugDump
    }
}
