prototype(Milly.Crudforms:Template.Index) < prototype(Neos.Fusion:Component) {

    @propTypes {
        style = ${PropTypes.oneOf(['narrow', 'wide'])}
        pagination = ${PropTypes.arrayOf(PropTypes.getAny())}
        objects = ${PropTypes.arrayOf(PropTypes.getAny())}
    }

    @context {
        crudFormsModelClass = ${crudFormsModelClass}
        configuration = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, null, 'index')}
        crudFormsView = 'index'
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudForms.defaultTheme'))}
    }

    style = 'wide'

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            attributes.style = "display: flex; justify-content: space-between;"
            content = ${configuration.label.many ? configuration.label.many : configuration.label.one + ' (' + objectCount + ')'}
        }
    }

    main = Neos.Fusion:Join {
        filterForm = afx`
            <Neos.Fusion.Form:Form form.target.action="index" attributes.class={'filter'} attributes.id={String.md5(crudFormsModelClass)+'-filter'}/>
        `
        indexTable = Milly.Crudforms:Component.IndexTable {
            objects = ${objects}
            disablePagination = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, 'views.index.options.pagination.disable')}
        }
    }

    footer = Neos.Fusion:Join {
        newButton = Milly.Crudforms:Component.ActionUi.New {
            @if.notReadonly = ${!configuration.ui.readonly}
            object = ${object}
            showLabel = true
            modelClassName = ${crudFormsModelClass}
        }
        export = Milly.Crudforms:Component.ActionUi.ExportCsv
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
            classPath = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Neos.Fusion:Case {
                            narrow {
                                condition = ${props.style=='narrow'}
                                renderer = Milly.Crudforms:Helper.CssClassRenderer {
                                    classPath = 'template.index.main.content.first.narrowClass'
                                }
                            }
                            wide {
                                condition = ${props.style=='wide'}
                                renderer = Milly.Crudforms:Helper.CssClassRenderer {
                                    classPath = 'template.index.main.content.first.wideClass'
                                }
                            }
                        }
                        content = ${props.main}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
        @process.debugDump = Neos.Fusion:DebugDump
    }
}
