prototype(Milly.Crudforms:Template.Show) < prototype(Neos.Fusion:Component) {

    @context {
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudForms.defaultTheme'))}
    }

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            content = Milly.Crudforms:Component.Label.ObjectLabel {
                object = ${object}
                showClassLabel = 'one'
            }
        }
    }

    main = Neos.Fusion:DataStructure {
        first = Neos.Fusion:Join {
            showTable = Milly.Crudforms:Component.ShowProperties {
                object = ${object}
            }
        }
        second = Neos.Fusion:Join {
            relations = Milly.Crudforms:Component.ShowRelations {
                object = ${object}
                relations = ${Milly.CrudForms.getConfigurationByClass(Type.className(object), 'relations', 'show')}
            }
        }
    }

    footer = Neos.Fusion:Join {
        editButton = Milly.Crudforms:Component.ActionUi.Edit {
            @if.notReadonly = ${!Milly.CrudForms.getConfigurationByClass(Type.className(object), 'ui.readonly')}
            object = ${object}
            showLabel = true
        }

        translateButton = Milly.Crudforms:Component.ActionUi.Translate {
            @if.notReadonly = ${!Milly.CrudForms.getConfigurationByClass(Type.className(object), 'ui.readonly') && Traduki.Entity.isTranslatable(object)}
            object = ${object}
            showLabel = true
        }
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
            path = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.first.class'
                        }
                        content = ${props.main.first}
                    }
                    second = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.second.class'
                        }
                        content = ${props.main.second}
                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
        @process.debugDump = Neos.Fusion:DebugDump
    }
}
