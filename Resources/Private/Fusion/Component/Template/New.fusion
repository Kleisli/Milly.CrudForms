prototype(Milly.Crudforms:Template.New) < prototype(Neos.Fusion:Component) {

    @context {
        crudFormsModelClass = ${crudFormsModelClass}
        configuration = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, null, 'new')}
        crudFormsView = 'new'
        millyCssClassSet = ${this.millyCssClassSet ? this.millyCssClassSet : 'neos'}
    }

    preset = Neos.Fusion:DataStructure

    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            content = Neos.Fusion:Join {
                label = ${configuration.label.one}
                create = ' erstellen&nbsp;'
                parent = Milly.Crudforms:Component.Label.ObjectLabel {
                    @if.hasParent = ${parent}
                    @process.wrap = ${'fÃ¼r ' + value}
                    object = ${parent}
                    showClassLabel = 'one'
                }
            }
        }
    }

    main = Neos.Fusion:DataStructure {
        second = Neos.Fusion:Join
    }

    footer = Neos.Fusion:Join {
        saveButton = Neos.Fusion.Form:Button {
            attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                classPath = 'template.new.createButton.class'
            }
            attributes.form = "new"
            content = Neos.Fusion:Join {
              icon = Milly.CrudForms:Atom.Icon.Save
              text = '&nbsp;Save'
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
            classPath = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                            classPath = 'layout.container.main.content.first.class'
                        }
                        content = Neos.Fusion:Join {
                            newForm = Milly.Crudforms:Component.NewForm {
                                properties = ${configuration.properties}
                                preset = ${props.preset}
                            }
                        }
                    }
                    second = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                            classPath = 'layout.container.main.content.second.class'
                        }
                        content = ${props.main.second}

                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.CssClassRenderer {
                    classPath = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
    }
    @process.debugDump = Neos.Fusion:DebugDump
}

