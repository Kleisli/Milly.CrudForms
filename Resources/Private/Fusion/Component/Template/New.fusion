prototype(Milly.Crudforms:Template.New) < prototype(Neos.Fusion:Component) {

    @context {
        millyCrudTheme = ${this.millyCrudTheme ? this.millyCrudTheme : (millyCrudTheme ? millyCrudTheme : Configuration.setting('Milly.CrudForms.defaultTheme'))}
    }

    preset = Neos.Fusion:DataStructure
    header = Neos.Fusion:Join {
        title = Neos.Fusion:Tag {
            tagName = 'legend'
            content = Neos.Fusion:Join {
                label = Milly.Crudforms:Component.Label.ClassLabel {
                    className = ${crudFormsModelClass}
                    showClassLabel = 'one'
                }
                create = ' erstellen&nbsp;'
                parent = Milly.Crudforms:Component.Label.ObjectLabel {
                    @if.hasParent = ${parent}
                    @process.wrap = ${'fÃ¼r ' + value}
                    object = ${parent}
                    showClassLabel = 'one'
                }
            }
        }
    }

    main = Neos.Fusion:DataStructure {
        second = Neos.Fusion:Join
    }

    footer = Neos.Fusion:Join {
        createButton = Milly.Crudforms:Component.ActionUi.CreateButton
    }

    renderer = Neos.Fusion:Tag {
        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
            path = 'layout.container.class'
        }
        content = Neos.Fusion:Join {
            header = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.header.class'
                }
                content = ${props.header}
            }
            main = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.main.class'
                }
                content = Neos.Fusion:Join {
                    first = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.first.class'
                        }
                        content = Neos.Fusion:Join {
                            newForm = Milly.Crudforms:Component.NewForm {
                                properties = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, 'properties', 'new')}
                                parent.@if.hasParent = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, 'parent')}
                                parent = ${Type.isObject(parent) ? Milly.Object.identifier(parent) : parent}
                                parentProperty = ${Milly.CrudForms.getConfigurationByClass(crudFormsModelClass, 'parent')}

                                preset = ${props.preset}
                            }
                        }
                    }
                    second = Neos.Fusion:Tag {
                        attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                            path = 'layout.container.main.content.second.class'
                        }
                        content = ${props.main.second}

                    }
                }
            }
            footer = Neos.Fusion:Tag {
                attributes.class = Milly.Crudforms:Helper.ThemeRenderer {
                    path = 'layout.container.footer.class'
                }
                content = ${props.footer}
            }
        }
    }
    @process.debugDump = Neos.Fusion:DebugDump
}

